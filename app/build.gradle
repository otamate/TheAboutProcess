apply plugin: 'com.android.application'

import org.tmatesoft.svn.core.wc.*

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.otamate.android.theaboutprocess"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'GENERATOR', '"' + getGenerator() + '"'
        buildConfigField 'String', 'BUILD_DATE', '"' + getDate() + '"'
        buildConfigField 'String', 'USERNAME', '"' + getUsername() + '"'
        buildConfigField 'String', 'SVN_REVISION', '"' + getSvnRevision() + '"'
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            sourceSets.debug.resources.srcDirs = ['src/debug/res']
            buildConfigField 'Boolean', 'BUILD_EXPIRES', 'true'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'BUILD_EXPIRES', 'true'
        }
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('dd-MMM-yyyy h:mm a')
    return formattedDate
}

def getGenerator() {
    return System.getenv("BUILD_NUMBER") ?: "dev"
}

def getUsername() {
    return System.getenv("USERNAME") == null ? "CI (" + 'hostname'.execute().text.trim() + ")" : System.getenv("USERNAME")
}

def getSvnRevision(){
    ISVNOptions options = SVNWCUtil.createDefaultOptions(true)
    SVNClientManager clientManager = SVNClientManager.newInstance(options)
    SVNStatusClient statusClient = clientManager.getStatusClient()
    SVNStatus status = statusClient.doStatus(projectDir, false)
    SVNRevision revision = status.getRevision()
    return revision.getNumber()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
